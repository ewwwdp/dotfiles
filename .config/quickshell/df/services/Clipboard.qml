pragma Singleton
pragma ComponentBehavior: Bound

import QtQuick
import Quickshell
import Quickshell.Io
import qs.core

Singleton {
    id: root

    property ListModel list: ListModel {}

    property bool fetchListProc: false
    property bool pendingListReset: false
    signal listUpdated

    property var filePath: Directories.clipboardPath
    property var pinnedEntries: []
    property var pinnedIds: []
    property var imageQueue: []

    FileView {
        id: pinFileView
        path: Qt.resolvedUrl(root.filePath)
        onLoaded: {
            let entries;
            try {
                entries = JSON.parse(pinFileView.text());
            } catch (e) {
                console.error("[Clipboard] Failed to parse pinned entries:", e);
                entries = [];
            }
            root.setPinnedEntries(entries);
            root.remapPinnedEntries();
        }
        onLoadFailed: error => {
            if (error == FileViewError.FileNotFound) {
                root.setPinnedEntries([]);
                savePinned();
            } else {
                console.error("[Clipboard] Error loading pins: " + error);
            }
        }
    }

    Process {
        id: decodeProc
        property int targetId: -1
        property int targetIndex: -1

        command: ['sh', '-c', `cliphist decode ${targetId} | base64 -w 0`]

        stdout: StdioCollector {
            id: imageDecoder
        }

        onExited: function (exitCode) {
            if (exitCode === 0 && targetIndex >= 0 && targetIndex < root.list.count) {
                var base64 = imageDecoder.text.trim();
                var item = root.list.get(targetIndex);
                var mimeType = "image/" + item.binaryType;
                root.list.setProperty(targetIndex, "previewSource", "data:" + mimeType + ";base64," + base64);
            } else if (exitCode !== 0) {
                console.error("cliphist decode failed with code:", exitCode);
            }

            processNextImage();
        }
    }

    Process {
        id: fetchListProc
        command: ['cliphist', 'list']
        running: root.fetchListProc

        stdout: StdioCollector {
            onStreamFinished: {
                root.fetchListProc = false;
                root.parseClipboardList(this.text);
            }
        }
    }

    Process {
        id: copyProc
        property int targetId: -1
        command: ['sh', '-c', `cliphist decode ${targetId} | wl-copy`]

        onExited: function (exitCode) {
            if (exitCode !== 0) {
                console.error("cliphist decode/copy failed with code:", exitCode);
            }
        }
    }

    Process {
        id: deleteProc
        property int targetId: -1
        command: ['sh', '-c', `echo ${targetId} | cliphist delete`]

        onExited: function (exitCode) {
            if (exitCode === 0) {
                root.fetchListProc = true;
            } else {
                console.error("cliphist delete failed with code:", exitCode);
            }
        }
    }

    Process {
        id: wipeProc
        command: ['cliphist', 'wipe']

        onExited: function (exitCode) {
            if (exitCode === 0) {
                root.list.clear();
            } else {
                console.error("cliphist wipe failed with code:", exitCode);
            }
        }
    }

    Process {
        id: batchDeleteProc
        property string ids: ""
        command: ['sh', '-c', `printf '%s\\n' ${ids} | cliphist delete`]

        onExited: function (exitCode) {
            if (exitCode === 0) {
                root.fetchListProc = true;
            } else {
                console.error("cliphist batch delete failed with code:", exitCode);
            }
        }
    }

    function parseClipboardList(text) {
        var lines = text.trim().split('\n');
        var newList = [];

        for (var i = 0; i < lines.length; i++) {
            var line = lines[i].trim();
            if (line.length === 0)
                continue;
            var tabIndex = line.indexOf('\t');
            if (tabIndex === -1)
                continue;
            var id = parseInt(line.substring(0, tabIndex));
            var content = line.substring(tabIndex + 1);

            var binaryMatch = content.match(/\[\[ binary data (.+?) ([a-z]+) (.+?) \]\]/);
            var isBinary = binaryMatch !== null;

            var entry = {
                id: id,
                content: content,
                isBinary: isBinary,
                binaryType: isBinary ? binaryMatch[2] : "",
                binarySize: isBinary ? binaryMatch[1] : "",
                binaryDimensions: isBinary ? binaryMatch[3] : "",
                previewSource: ""
            };

            newList.push(entry);
        }

        updateList(newList);
    }

    function updateList(newList) {
        root.list.clear();
        for (var j = 0; j < newList.length; j++) {
            root.list.append(newList[j]);
        }

        remapPinnedEntries();

        root.listUpdated();
    }

    function remapPinnedEntries() {
        var newEntries = [];
        for (var p = 0; p < pinnedEntries.length; p++) {
            var entry = pinnedEntries[p];
            var found = false;
            for (var i = 0; i < root.list.count; i++) {
                if (root.list.get(i).id === entry.id) {
                    newEntries.push(entry);
                    found = true;
                    break;
                }
            }
            if (!found) {
                for (var i = 0; i < root.list.count; i++) {
                    if (root.list.get(i).content === entry.content) {
                        newEntries.push({id: root.list.get(i).id, content: entry.content});
                        found = true;
                        break;
                    }
                }
                if (!found) {
                    newEntries.push(entry);
                }
            }
        }
        setPinnedEntries(newEntries);
    }

    function setPinnedEntries(entries) {
        pinnedEntries = entries;
        var ids = [];
        for (var i = 0; i < entries.length; i++) {
            ids.push(entries[i].id);
        }
        pinnedIds = ids;
    }

    function copyToClipboard(id) {
        copyProc.targetId = id;
        copyProc.running = true;
    }

    function deleteEntry(id) {
        deleteProc.targetId = id;
        deleteProc.running = true;
    }

    function loadImagePreview(index) {
        if (index < 0 || index >= root.list.count)
            return;
        var item = root.list.get(index);
        if (!item.isBinary)
            return;
        if (item.previewSource !== "")
            return;
        imageQueue.push({
            id: item.id,
            index: index
        });

        if (!decodeProc.running) {
            processNextImage();
        }
    }

    function processNextImage() {
        if (imageQueue.length === 0)
            return;
        if (decodeProc.running)
            return;
        var next = imageQueue.shift();

        if (next.index >= 0 && next.index < root.list.count) {
            var item = root.list.get(next.index);
            if (item.isBinary && item.previewSource === "") {
                decodeProc.targetId = next.id;
                decodeProc.targetIndex = next.index;
                decodeProc.running = true;
                return;
            }
        }

        processNextImage();
    }

    function savePinned() {
        pinFileView.setText(JSON.stringify(root.pinnedEntries));
    }

    function pinEntry(id, content) {
        for (var i = 0; i < pinnedEntries.length; i++) {
            if (pinnedEntries[i].id === id) return;
        }
        setPinnedEntries(pinnedEntries.concat([{id: id, content: content}]));
        savePinned();
    }

    function unpinEntry(id) {
        var idx = -1;
        for (var i = 0; i < pinnedEntries.length; i++) {
            if (pinnedEntries[i].id === id) {
                idx = i;
                break;
            }
        }
        if (idx >= 0) {
            var arr = pinnedEntries.slice();
            arr.splice(idx, 1);
            setPinnedEntries(arr);
            savePinned();
        }
    }

    function isPinned(id) {
        for (var i = 0; i < pinnedEntries.length; i++) {
            if (pinnedEntries[i].id === id) return true;
        }
        return false;
    }

    function findIndex(id) {
        for (var i = 0; i < root.list.count; i++) {
            if (root.list.get(i).id === id)
                return i;
        }
        return -1;
    }

    function clearHistory() {
        var ids = [];
        for (var i = 0; i < root.list.count; i++) {
            var item = root.list.get(i);
            if (!isPinned(item.id)) {
                ids.push(item.id);
            }
        }
        if (ids.length > 0) {
            batchDeleteProc.ids = ids.join(' ');
            batchDeleteProc.running = true;
        }
    }

    Component.onCompleted: {
        pinFileView.reload();
        root.fetchListProc = true;
    }
}
